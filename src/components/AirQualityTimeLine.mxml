<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:display="flash.display.*"  height="70"
	backgroundColor="0xDDCCCCCC" backgroundAlpha="0.8" 
	horizontalScrollPolicy="off" verticalScrollPolicy="off">
	
	<mx:VRule id="tipLine" top="{margin}" bottom="{margin}" visible="false"/>
	
	<mx:Canvas id="container" width="100%" height="100%" resize="{refresh()}" 
		mouseOver="{handleMouseMove(event)}" mouseOut="{handleMouseOut(event)}" 
		mouseMove="{handleMouseMove(event)}" click="{handleMouseClick(event)}" 
		doubleClick="{handleDoubleClick}"
		horizontalScrollPolicy="off" verticalScrollPolicy="off"
		doubleClickEnabled="true" 
		alpha="{plotCheck.selected ? 1 : 0.4}" enabled="{plotCheck.selected}"/>
	
	<mx:CheckBox id="plotCheck" x="15" y="5" selected="true" visible="false"/>
	<mx:LinkButton id="remove" click="{dispatchEvent(new CSEvent(CSEvent.REMOVE_CLICKED))}" label="x" x="8" y="5" 
		enabled="{plotCheck.selected}" toolTip="remove track" />
	<mx:LinkButton id="zIn" click="{zoomIn()}" label="+" x="28" y="5" 
		enabled="{plotCheck.selected}" toolTip="zoom in" />
	<mx:LinkButton id="zOut" click="{zoomOut()}" label="-" left="50" top="5" 
		enabled="{plotCheck.selected}" toolTip="zoom out"/>
	<mx:Label text="{displayName}" left="75" fontFamily="Helvetica" color="#898989" fontSize="15" fontWeight="normal" top="5" selectable="true"/>
	
	<mx:Script>
		<![CDATA[
			import events.CSEvent;
			import etc.AirQualityColors;
			import events.DataPointEvent;
			import mx.controls.ToolTip;
			import mx.core.IToolTip;
			import mx.managers.ToolTipManager;
			import fl.events.ScrollEvent;
			import fl.controls.ScrollBarDirection;
			import fl.controls.ScrollBar;
			import __AS3__.vec.Vector;
			
			//------------- Public variables -----------------------------------------------
			[Bindable]
			public var displayName:String;
			public var xDataField:String = 'time';
			public var yDataField:String = 'value';
			public var pollutant:String = AirQualityColors.CO_8HR;
			public var margin:Number = 15;
			public var pointDiameter:Number = 6;
			public var pointOverlapTolerance:Number = 6;
			public var zoomPercentage:Number = 0.6;
			public var numMouseOverAdjacents:uint = 15;
			
			
			/**
			 * A data object should be an Object containing
			 *  sets of points (as Vector.<Object>s) 
			 *  indexed by the URI of that set of points.  
			 * @param d
			 * 
			 */	
			override public function get data():Object{return _data;}
			override public function set data(d:Object):void{
				_data = d;
				refresh(true);
			}
			
			//------------- Data Management variables -----------------------------------------------
			protected var _data:Object;
			protected var yMin:Number;		//the lowest data value on the y axis
			protected var yMax:Number; 		//the highest data value on the y axis
			protected var xMin:Number;		//the lowest data value on the x axis
			protected var xMax:Number;		//the lowest data value on the x axis
			
			protected var xBottom:Number;	//the bottom end of the current display x-axis
			protected var xTop:Number;		//the top end of the current display x-axis

			protected var _pSetBounds:Object = {}
			
			protected var _bitmap:Bitmap;
			protected var _mask:Sprite = new Sprite();
			protected var _hScroll:ScrollBar = new ScrollBar();
						
			


		
			//-------------Public Methods -----------------------------------------------------

			public function refresh(force:Boolean=false):void{
				if(force)calculateMinMax();
				if(isNaN(yMin) || isNaN(yMax)) return;
				else plotData();
			}
			
			
			//------------- Protected Methods -----------------------------------------------------
				
			protected function plotData(e:Event=null):void{
				
				if(!container) return;

				//clear previously drawn bitmaps
				while(container.rawChildren.numChildren > 0)container.rawChildren.removeChildAt(0);

				//If no data do nothing
				if(!data || data.length == 0) return;
				
				
				//prep the shape object and bitmap data we'll use to draw the points
				var plotShape:Shape = new Shape();
				var translationMatrix:Matrix = new Matrix();
				var plotBitmapData:BitmapData = new BitmapData(container.width * 2,container.height,true,0x00FFFFFF);
											
				var prevPoint:Object;
				var prevPt:Point;
				
				//draw outside the current range so that scrolling 
				// cuts less abruptly
				var curRange:Number = xTop - xBottom;
				var xbd:Number = Math.max(xMin,xBottom - curRange);
				var xtd:Number = Math.min(xMax,xTop + curRange);
				
				//allocate space for each point set
				_pSetBounds = {};
				var numTracks:uint = 0;
				for(var n:String in data) numTracks++;
				var psHeight:Number = (container.height)/numTracks;
				var psBounds:Rectangle = new Rectangle(container.x,container.y,container.width,psHeight);

				plotBitmapData.lock();
				
				//for each of the sets of points...
				for(var psName:String in _data){
					var pSet:Vector.<Object> = _data[psName];
					
					//set bounds for the set
					_pSetBounds[psName] = psBounds.clone();
					plotAxes(plotBitmapData,psBounds);
					psBounds.y += psHeight;
					
					for(var i:int=0; i < pSet.length; i++){
						var point:Object = pSet[i]; 
						if(point.time < xbd || point.time > xtd) continue;
						if(!point.cat)point.cat = AirQualityColors.getAQICategoryForValue(pollutant,point.value);
					
						//determine location
						var dPt:Point = dataToPosition(point);
			
						//skip if position, category not different from last
						if(prevPoint && prevPoint.cat == point.cat 
								&& Math.abs(prevPt.x - dPt.x) < pointOverlapTolerance 
								&& Math.abs(prevPt.y - dPt.y) < pointOverlapTolerance){
							continue;
						}  
										
						//draw the point
						var color:uint = AirQualityColors.getColorForValue(pollutant,point.value);
						plotShape.graphics.clear();
						plotShape.graphics.lineStyle(0.5,0xffffff,0.6);
						plotShape.graphics.beginFill(color,0.6);
						plotShape.graphics.drawCircle(0,0,pointDiameter/2);
	        			plotShape.graphics.endFill();
	        			//grey out the centers of points with no lat/lon
	        			if(!point.lat ||  point.lat == "None"){
	        				plotShape.graphics.lineStyle(0,0,0);
	        				plotShape.graphics.beginFill(0x777777,0.8);
							plotShape.graphics.drawCircle(0,0,pointDiameter/5);
	        				plotShape.graphics.endFill();
	        			}
						translationMatrix.tx = dPt.x;
						translationMatrix.ty = dPt.y;
						
						plotBitmapData.draw(plotShape,translationMatrix);
											
						prevPoint = point;
						prevPt = dPt;
					}
				}
				
				plotBitmapData.unlock();
				
				_bitmap = new Bitmap(plotBitmapData);
				_bitmap.x = -(container.width / 2);
				
				container.rawChildren.addChild(_bitmap);
				
				//draw and position the scrollBar and mask
				setupMask();
				setupScrollBar();
			}
			
			
			protected function plotAxes(bd:BitmapData,bounds:Rectangle):void{
				var plotShape:Shape = new Shape();
				
				//plot x baseline
				plotShape.graphics.lineStyle(0.5,0x999999);
				plotShape.graphics.moveTo(0,bounds.bottom - 2 * margin + pointDiameter/2);
				plotShape.graphics.lineTo(bd.width,bounds.bottom - 2 * margin + pointDiameter/2);
				
				var startDate:Date = new Date(xBottom * 1000);
				startDate.setFullYear(startDate.fullYear,startDate.month,startDate.day + 1);
				startDate.setHours(0,0,0,0);
				
				//plot days
				var d:Number = startDate.time / 1000;
				plotShape.graphics.lineStyle(0.5,0x999999,0.5);
				while(d < xTop){
					var dx:Number = (d - xBottom) * (bounds.width - 2 * margin) / 
							(xTop - xBottom) + bounds.width / 2 + margin;
					plotShape.graphics.moveTo(dx,bounds.bottom - margin);
					plotShape.graphics.lineTo(dx,bounds.top - margin);
					d += (24 * 3600);
				}
				
				//plot hours
				d = startDate.time / 1000;
				plotShape.graphics.lineStyle(0.5,0x999999,0.2);
				while(d < xTop){
					var dx:Number = (d - xBottom) * (bounds.width - 2 * margin) / 
							(xTop - xBottom) + bounds.width / 2 + margin;
					plotShape.graphics.moveTo(dx,bounds.bottom - margin);
					plotShape.graphics.lineTo(dx,bounds.top - margin);
					d += 3600;
				}
				
				//plot hours
				bd.draw(plotShape);				
				
			}
		

			/**
			 * Setup and place the mask for the bitmap 
			 */ 
			protected function setupMask():void{
				_mask.graphics.clear();
				_mask.graphics.beginFill(0xFFFFFF);
				_mask.graphics.drawRect(margin - pointDiameter / 2,margin - pointDiameter / 2,
					container.width - 2 * margin + pointDiameter, container.height - margin);
				_mask.graphics.endFill();
				
				container.rawChildren.addChild(_mask);
				
				if(container.mask != _mask)container.mask = _mask;
			}
			
			
			/**
			 * Set up and place the scroll bar
			 */
			protected function setupScrollBar():void{
				_hScroll.direction = ScrollBarDirection.HORIZONTAL;
				_hScroll.x = margin - pointDiameter / 2;
				_hScroll.y = container.height - _hScroll.height - margin + 10;
				_hScroll.width = container.width - 2 * margin + pointDiameter;
				
				var range:Number = xTop - xBottom;
				
				_hScroll.setScrollProperties(range,xMin,xMax-range,range / 10);
				_hScroll.scrollPosition = xBottom;
				
				if(!_hScroll.hasEventListener(ScrollEvent.SCROLL)){
					_hScroll.addEventListener(ScrollEvent.SCROLL, handleScroll);
				}
								
				container.rawChildren.addChild(_hScroll);
			}
			
			protected var _scrollTimer:Timer;
			
			
			protected function handleScroll(se:ScrollEvent):void{	
				var range:Number = xTop - xBottom;
				_bitmap.x -= ((se.position - xBottom) * (container.width - 2 * margin) / range);  
				
				xBottom = Math.max(_hScroll.scrollPosition,xMin);
				xTop = Math.min(xBottom + range,xMax);
				
				if(_scrollTimer && _scrollTimer.running){
					_scrollTimer.reset();
					_scrollTimer.start();
				} 
				else{
					_scrollTimer = new Timer(300);
					_scrollTimer.addEventListener(TimerEvent.TIMER,function(e:Event):void{
							_scrollTimer.stop();
							refresh();
						});
					_scrollTimer.start();
				}
					
			}
			
			protected function zoomIn():void{
				var zoomRange:Number = (xTop - xBottom) * zoomPercentage;
				var midPoint:Number = xBottom + (xTop - xBottom) / 2;
				xBottom = Math.max(xMin,int(midPoint - zoomRange / 2));
				xTop = Math.min(xMax,int(midPoint + zoomRange / 2));
				refresh();
			}
			
			protected function zoomOut():void{
				var zoomRange:Number = (xTop - xBottom) / zoomPercentage;
				var midPoint:Number = xBottom + (xTop - xBottom) / 2;
				xBottom = Math.max(xMin,midPoint - zoomRange / 2);
				xTop = Math.min(xMax,midPoint + zoomRange / 2);
				refresh();
			}
			
			protected function dataToPosition(pointData:Object):Point{
				if(!(_pSetBounds[pointData.sourceURI] is Rectangle)) return null;
				var sBounds:Rectangle = _pSetBounds[pointData.sourceURI];
				
				return new Point((pointData[xDataField] - xBottom) * (sBounds.width - 2 * margin) /
						 	(xTop - xBottom) + sBounds.width / 2 + margin,
						 (sBounds.height - 2 * margin - 10) * (1 - (pointData[yDataField] - yMin) /
						 	(yMax - yMin)) + sBounds.y + margin); 
			}
			
			
			
			protected function xPosToDataPts(x:Number):Array{
				var position:Number = x - margin;
				var overXPos:int = xBottom + Math.floor(position * (xTop - xBottom)/(container.width - 2 * margin));
				var pts:Array = []
				for each(var ps:Vector.<Object> in _data){
					for(var p:int=0;p<ps.length; p++){
						if(Math.abs(ps[p][xDataField] - overXPos) < 10){
							pts.push(ps[p]);
							break;
						} 
					}					
				}
				return pts; 
			}
			
			//FIXME: O(n) search is not efficient
			protected function dataToPointNum(pointData:Object):int{
				var sourceData:Vector.<Object> = data[pointData.sourceURI];
				for(var i:int = 0; i < sourceData.length; i++){
					if(sourceData[i] == pointData) return i;
				}
				return -1;
			}
			
			/****************************** Tooltipping Methods ************************************/
			protected var _plotTip:ToolTip;
			
			/**
			 * Draws and positions a tooltip for the given datapoint.
			 */
			public function drawDataTip(dataPoint:Object):void{
				
				var ptPosition:Point = dataToPosition(dataPoint);
				if(!ptPosition) return;
				ptPosition = container.localToGlobal(ptPosition);
				if(!_plotTip)_plotTip = ToolTipManager.createToolTip(dataPoint.value,ptPosition.x,ptPosition.y) as ToolTip;
			
				//color the tip and add additional text if possible
				if(dataPoint.value){
					var cat:String = AirQualityColors.getAQICategoryForValue(pollutant,dataPoint.value);
					var color:uint = AirQualityColors.getColorForAQICategory(cat);
					_plotTip.setStyle("backgroundColor",color);
					_plotTip.setStyle("backgroundAlpha",0.7);
					var time:Number = Number(dataPoint.time * 1000);
					var date:Date = new Date(time);
					_plotTip.text = (date + "\n" + 
						dataPoint.value + " (" + cat + ")" +
						((dataPoint.lat == null || dataPoint.lat == "None") ? "\n!NO GPS DATA" : ""));
					var rightInset:Number = Math.max(0, ptPosition.x + _bitmap.x -
						 container.localToGlobal(new Point(container.width - _plotTip.width)).x);
					_plotTip.x = ptPosition.x + _bitmap.x - rightInset;
					_plotTip.y = ptPosition.y;
					
					_plotTip.graphics.clear();
					
					//Overplot nearby points as part of the tooltip
					var pn:int = dataToPointNum(dataPoint);
					if(pn != -1){
						for(var pi:int = pn - numMouseOverAdjacents; pi < pn + numMouseOverAdjacents; pi++){
							if(pi < 0 || pi >= data.length) continue;
							var adjPt:Object = data[pi];
							if(adjPt && adjPt.lat != null && adjPt.lat !="None"){
								var aColor:uint = AirQualityColors.getColorForValue(pollutant,adjPt.value);
								var aPosition:Point = container.localToGlobal(dataToPosition(adjPt));
								_plotTip.graphics.lineStyle(2,0xffffff,0.7);
								_plotTip.graphics.beginFill(aColor,1);
								_plotTip.graphics.drawCircle(aPosition.x - ptPosition.x, aPosition.y - ptPosition.y,pointDiameter - (pointDiameter/2 * Math.abs(pi - pn)/numMouseOverAdjacents));
				    			_plotTip.graphics.endFill();								
							}
						}
					}
					
					//Plot an accentuated version of the point as part of the tooltip
					_plotTip.graphics.lineStyle(2,0xffffff,0.7);
					_plotTip.graphics.beginFill(color,1);
					_plotTip.graphics.drawCircle(rightInset,0,pointDiameter);
	    			_plotTip.graphics.endFill();
	    			_plotTip.graphics.beginFill(0xffffff,1);
	    			_plotTip.graphics.drawCircle(rightInset,0,pointDiameter/2);
	    			_plotTip.graphics.endFill();
	    			
	    			
	    			
	    			
				}
				else _plotTip.text = "No Data";
			}	
				
				
			public function discardActiveDataTip():void{
				if(_plotTip){
					ToolTipManager.destroyToolTip(_plotTip);
					_plotTip = null;
					dispatchEvent(new DataPointEvent(DataPointEvent.UNHOVER));
				}
			}
			
			protected function handleMouseClick(me:MouseEvent):void{
				//if we've clicked on some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//calculate the location of the click and locate the nearest matching point
				var overPts:Array = xPosToDataPts(me.localX);
							
				//If we have a point, fire an event
				if(overPts.length > 0) dispatchEvent(new DataPointEvent(DataPointEvent.CLICK,overPts[0]));
			}
			
			protected function handleDoubleClick(me:MouseEvent):void{
				
				//if we've clicked on some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//calculate the location of the click and locate the nearest matching point
				var overPts:Array = xPosToDataPts(me.localX);
							
				//If we have a point, zoom
				if(overPts.length > 0) zoomIn();
			}
			
			
			protected function handleMouseMove(me:MouseEvent):void{
				
				//if we've moused over some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//calculate the location we're currently mousing over and locate the nearest matching point
				var overPts:Array = xPosToDataPts(me.localX);
				
				
				//If we have a point, mark it
				if(overPts.length > 0){
					var overPt:Object = overPts[0];
					drawDataTip(overPt);
					tipLine.x = me.localX;
					tipLine.visible = true;
					dispatchEvent(new DataPointEvent(DataPointEvent.HOVER,overPt));
				} 
				//If no points, throw away any tip we might have
				else{
					discardActiveDataTip();
					tipLine.visible = false;
				} 
				
			}
			
			protected function handleMouseOut(me:MouseEvent):void{
				tipLine.visible = false;
				discardActiveDataTip();
			}
			
			
			/****************************** Helper Methods ************************************/
			
			protected function calculateMinMax():void{
				
				for each(var ds:Vector.<Object> in data){
					//Find mins and maxes
					for each(var o:Object in ds){

						var xVal:Number = Number(o[xDataField]);
						if(isNaN(xMin) || xVal < xMin) xMin = xVal;
						if(isNaN(xMax) || xVal > xMax) xMax = xVal;

						var yVal:Number = Number(o[yDataField]);
						if(isNaN(yMin) || yVal < yMin) yMin = yVal;
						if(isNaN(yMax) || yVal > yMax) yMax = yVal;
					}
				}
				xBottom = xMin;
				xTop = xMax;
			}
		]]>
	</mx:Script>
	
	
</mx:Canvas>
