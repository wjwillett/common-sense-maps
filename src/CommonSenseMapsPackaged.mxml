<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	xmlns:flex="com.modestmaps.flex.*" implements="commentspace.IStatefulVis" 
	horizontalScrollPolicy="off" verticalScrollPolicy="off" xmlns:ns1="components.*" 
	backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#FFFFFF, #FFFFFF]">
	
	<mx:Canvas id="container" width="100%" height="100%" backgroundColor="0xffffff" 
		horizontalScrollPolicy="off" verticalScrollPolicy="off" creationComplete="{setup()}"/>
	
	<mx:VDividedBox left="15" top="15" right="15" width="100" height="100%">
		<ns1:AirQualityTimeLine id="timeline" width="100%" height="200" creationComplete="{setup()}"/>
		<mx:VBox width="100%">
			<mx:HBox width="100%" verticalAlign="middle">
				<mx:Button label="+ load additional data" click="{triggerLoadingPopup()}"  
					height="25"/>
				<mx:Button label="refresh" click="{reloadDataSets()}"  
					toolTip="reloads all loaded datasets a any new data"
					height="25"/>
				<mx:Button label="center map on last point" click="{centermap()}"  
					color="#62D000" fillAlphas="[1.0, 1.0]" fillColors="[#FFFFFF, #E6E6E6]"
					height="25"/>
				<mx:Canvas backgroundColor="#FFFFFF" borderStyle="solid" cornerRadius="5" width="135" borderColor="#FFFFFF" borderThickness="0" backgroundAlpha="0.75">
					<mx:CheckBox label="follow playback" id="followPlayback" color="#000000" fontSize="10" x="12" y="1" selected="true" fontWeight="bold"/>
				</mx:Canvas>
				<mx:ProgressBar id="progressBar" width="100%" visible="false" includeInLayout="false" indeterminate="true" mode="manual"/>	
			</mx:HBox>
			<ns1:AirQualityLegend top="230" left="15" width="150" height="160"/>
		</mx:VBox>
	</mx:VDividedBox>
	

	<mx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			import commentspace.events.EntityEvent;
			import mx.events.CollectionEventKind;
			import mx.events.CollectionEvent;
			import events.TimelineEvent;
			import events.DataSetEvent;
			import data.SelectionSet;
			import data.AirQualityDataSet;
			import __AS3__.vec.Vector;
			import components.DisclaimerPanel;
			import events.CSEvent;
			import events.DataPointEvent;
			import mx.managers.PopUpManager;
			import components.loading.DataLoadingPanel;
			import components.AirQualityMapLayer;
			import etc.AirQualityConstants;
			import mx.messaging.AbstractConsumer;
			import mx.effects.Zoom;
			import com.modestmaps.mapproviders.microsoft.MicrosoftHybridMapProvider;
			import com.modestmaps.events.MarkerEvent;
			import com.modestmaps.TweenMap;
			import com.modestmaps.extras.ui.Button;
			import mx.events.ResizeEvent;
			import com.modestmaps.events.MapEvent;
			import commentspace.StatefulVisEvent;
			import com.modestmaps.overlays.MarkerClip;
			import mx.events.MenuEvent;
			import mx.controls.Menu;
			import com.modestmaps.Map;
			import com.modestmaps.extras.ZoomSlider;
			import com.modestmaps.extras.MapControls;
			import flash.sampler.Sample;
			import com.modestmaps.geo.Location;
			
			/******** Embedded Assets **********************************************************/
			[Embed(source="assets/flag_blue.png")]
			protected const BlueFlagIcon:Class;
			[Embed(source="/assets/comment_flag.png")]
			protected const CommentIcon:Class;
			[Embed(source="assets/cross.png")]
			protected const CancelIcon:Class;
			
			/******** Source Data **********************************************************/
			[Embed(source = "assets/2009-03-31 (Berkeley - Pleasanton Drive).csv", mimeType="application/octet-stream")]
			protected const EmbeddedCOData:Class;
			[Embed(source = "assets/2009-07-07 (Berkeley PM + GPS).csv", mimeType="application/octet-stream")]
			protected const EmbeddedPMData:Class;
			
			/******** Constants **********************************************************/
			protected const CLICK_RADIUS:Number = 2;
			
			protected static const DESATURATION_MATRIX:Array = [
					0.3, 0.59, 0.3, 0, 0,
		            0.3, 0.59, 0.3, 0, 0,
		            0.3, 0.59, 0.3, 0, 0,
		            0, 0, 0, 0.5, 0
		        ];
			
			/******** Local Vars **********************************************************/
			protected var maxPoints:int = 1000;
			
			//the map!
			protected var map:TweenMap;
			
			//Clip the flags are drawn into
			protected var _flagsClip:MarkerClip;
			
			//Clip the textures are drawn into
			protected var _plotLayer:AirQualityMapLayer;
			
			//The backing data sets for the visualization
			protected var _dataSets:Vector.<AirQualityDataSet> = new Vector.<AirQualityDataSet>();
			
			//An object containing a list of selections 
			protected var _selections:SelectionSet = new SelectionSet();
			
			//flag to prevent updated events from being dispatched during panAndZoomTo runs
			protected var _isPanningAndZooming:Boolean = false; 
			
			protected var firstLoad:Boolean = false;
			protected function setup():void{
				if(!firstLoad) firstLoad = true;
				else{
					setupMap();
					setupTimeline();
				}
			}
			
			/******** Mapping Methods **********************************************************/
			protected function setupMap():void{
				
				map = new TweenMap();
				map.setMapProvider(new MicrosoftHybridMapProvider(10,19));
				map.grid.filters = [new ColorMatrixFilter(DESATURATION_MATRIX)];
				container.rawChildren.addChild(map);
				
				map.x = 0;
				map.y = 0;
				map.setSize(container.width,container.height);
				
				map.panDuration = 0.6;
				map.zoomDuration = 0.5;
				map.panAndZoomDuration = 0.6;
				
				_plotLayer = new AirQualityMapLayer(map);
				_plotLayer.dataSets = _dataSets;
				_plotLayer.selections = _selections;
				
				map.addChild(_plotLayer);
				
				map.addChild(new MapControls(map));

				//set the map center
				map.setCenterZoom(new Location(37.803,-122.298),14);
				
				//Listeners for updating state 
				map.addEventListener(MapEvent.STOP_PANNING, dispatchUpdated);
				map.addEventListener(MapEvent.STOP_ZOOMING, dispatchUpdated);
				map.addEventListener(CSEvent.PAN_AND_ZOOM_COMPLETE, dispatchUpdated);
				
				this.addEventListener(ResizeEvent.RESIZE,onResize);
				
				//Listener for zoom and pan
				map.addEventListener(MouseEvent.DOUBLE_CLICK,function(me:MouseEvent):void{
						var location:Location = map.pointLocation(map.globalToLocal(new Point(me.stageX,me.stageY)),map);
						panAndZoomTo(location,map.getZoom() + 1);
					});
				
				//A function for loading a default set of data		
				//TODO: make this cleaner
				var runOnStartup:Function = function():void{
					var request:URLRequest = new URLRequest(DataLoadingPanel.DATA_INDEX_XML_URL);
					var loader:URLLoader = new URLLoader(request);
					
					loader.addEventListener(Event.COMPLETE,function(e:Event):void{
							var indexString:String = loader.data;
							indexString = indexString.replace(/BASE_DB_QUERY_URL/gi,DataLoadingPanel.BASE_DB_QUERY_URL);
							indexString = indexString.replace('24HOURSAGO',int((new Date().time) / 1000 - 24 * 3600));
							indexString = indexString.replace('1WEEKAGO',int((new Date().time) / 1000 - 7 * 24 * 3600));
							var indexArray:Array = JSON.decode(indexString).defaults as Array;
							
							if(indexArray && indexArray.length > 0){
								var loading:int = indexArray.length;
								for each(var def:Object in indexArray){
									loadData(def.url,def.reading,def.name,function():void{ 
										loading--;
										if(loading == 0)dispatchComplete(); 
									});	
								}	
							}
						});
					};	
					
				var quickStartup:Function = function ():void{
					/* var quickdata:Object = {"name":"All Badges Carbon Monoxide (past 3 hours)",
						"url":DataLoadingPanel.BASE_DB_QUERY_URL +"?reading=mco&start_time=" + int((new Date().time) / 1000 - 3 * 3600),
						"reading":"co"}; */
					 var quickdata:Object = {"name":"Berkeley to Pleasanton CO",
						"source":EmbeddedCOData,
						"reading":"co"}; 
					/* var quickdata:Object = {"name":"Berkeley PM2.5",
						"source":EmbeddedPMData,
						"reading":"pm25"}; */
					loadData(quickdata.source,quickdata.reading,quickdata.name,function():void{centermap();dispatchComplete();});
					_plotLayer.pointDiameter = 20;
					_plotLayer.pointOverlapTolerance = 15;
					timeline.pointDiameter = 12;
					timeline.timeMultiplier = 60;
					map.setZoom(13);
				}
				
				
				/*A set of different options for what to do on startup. 1-4 are helpful when debugging*/
					
				//0.Production startup. Requires a click-through disclaimer before using the site	
				//triggerDisclaimerPopup(runOnStartup);
				
				//1.Alternative startup. Jump straight into a load
				//runOnStartup();
				
				//2.Alternative startup, loads a small test dataset
				quickStartup();
					
				//3.Alternative startup, just pops up the loader
				//triggerLoadingPopup();
				
				//4. Do nothing. Just let commentspace know we're loaded
				//dispatchComplete();	
					
			}
			
			protected function setupTimeline():void{

				timeline.dataSets = _dataSets;
				timeline.selections = _selections;
				
				//Add handlers for linked tooltipping between map and timeline
				var drawMapTip:Function = function(de:DataPointEvent=null):void{
						_plotLayer.drawDataTip(de.dataPoint);
					};
				var drawTimelineTip:Function = function(de:DataPointEvent=null):void{
						timeline.drawDataTip(de.dataPoint);
					};
				var discardMapTip:Function = function(de:DataPointEvent=null):void{
						_plotLayer.discardActiveDataTip();
					};
				var discardTimelineTip:Function = function(de:DataPointEvent=null):void{
						timeline.discardActiveDataTip();
					};
				timeline.addEventListener(DataPointEvent.HOVER,drawMapTip);
				timeline.addEventListener(DataPointEvent.UNHOVER,discardMapTip);
				_plotLayer.addEventListener(DataPointEvent.HOVER,drawTimelineTip);
				_plotLayer.addEventListener(DataPointEvent.UNHOVER,discardTimelineTip);
				
				//handlers for popup menus on map and timeline
				timeline.addEventListener(DataPointEvent.CLICK,function(de:DataPointEvent):void{
						discardTimelineTip();	
						createMenu(de.mousePosition,de.dataPoint);
						
					});
				_plotLayer.addEventListener(DataPointEvent.CLICK,function(de:DataPointEvent):void{
						discardMapTip();
						createMenu(de.mousePosition,de.dataPoint);
					});
				
				//handlers for dataset removal and hide/unhide
				var handleRemove:Function = function(de:DataSetEvent):void{			
						_plotLayer.refresh(true);
						timeline.refresh(true);
						dispatchUpdated();
					};
				var handleHide:Function = function(de:DataSetEvent):void{			
						timeline.refresh();
						_plotLayer.refresh(true);
						dispatchUpdated();
					};
				timeline.addEventListener(DataSetEvent.DATASET_REMOVED, handleRemove);
				timeline.addEventListener(DataSetEvent.DATASET_HIDDEN, handleHide);
				timeline.addEventListener(DataSetEvent.DATASET_UNHIDDEN, handleHide);
					
				//time scrubbing for map
				timeline.addEventListener(TimelineEvent.PLAYHEAD_MOVED,function(te:TimelineEvent):void{
						if(te.playheadTime){
							_plotLayer.maxTime = te.playheadTime;
							
							//Centers map on the last point
							//FIXME: This is a VERY quick implementation that loops through all the points every time.
							// optimize the search for the last point(e.g. only check since the last update) and 
							// smooth the playback	
							if(followPlayback.selected && _dataSets.length > 0 && (_dataSets[0] as AirQualityDataSet).data){
								var ds:AirQualityDataSet = (_dataSets[0] as AirQualityDataSet);
								
								//iterate through points
								for(var i:int=0; i < ds.data.length; i++){
									var point:Object = ds.data[i]; 
									//Save the index of the last point drawn at/after the playhead 							
									if(Number(point.time) > te.playheadTime && point.lat && point.lat != "None" && point.lat !=0){
										//panAndZoomTo(new Location(point.lat, point.lon),map.getZoom());
										map.setCenterZoom(new Location(Number(point.lat),Number(point.lon)),map.grid.zoomLevel);
										break;
									}
								}
							}
							_plotLayer.refresh();
						} 
					});
				
				//Listener for updating map min and max times on the map when timeline zooms and pans
				var timelineMoveHandler:Function = function(te:TimelineEvent):void{
						if(!isNaN(timeline.xTop)) _plotLayer.maxTime = timeline.xTop;
						if(!isNaN(timeline.xBottom)) _plotLayer.minTime = timeline.xBottom;
						_plotLayer.refresh();
						dispatchUpdated();
					};
				timeline.addEventListener(TimelineEvent.TIMELINE_ZOOMED,timelineMoveHandler);
				timeline.addEventListener(TimelineEvent.TIMELINE_SCROLLING,timelineMoveHandler);
				timeline.addEventListener(TimelineEvent.TIMELINE_SCROLLED,timelineMoveHandler);
				
				//Update state whenever the timeline stops (do not try to update during playback)
				timeline.addEventListener(TimelineEvent.PLAYHEAD_STOPPED,dispatchUpdated);
			}
			
			
			/**
			 * Loads data from a source and passes it to the timeline and map when complete.
			 *  Manages the progress bar while loading. 
			 */
			protected function loadData(source:Object,pollutant:String,name:String=null,onComplete:Function=null):void{
				
				//if this same source has already been loaded, remove it before reloading.
				for each(var d:AirQualityDataSet in _dataSets){
					if(d.dataURI == source) _dataSets.splice(_dataSets.lastIndexOf(d),1);
				}
				
				progressBar.visible = true;
				progressBar.includeInLayout = true;
				progressBar.minimum = 0;
				progressBar.setProgress(0,1);
				
				var handleProgress:Function = function(pe:ProgressEvent):void{
						progressBar.setProgress(pe.bytesLoaded,pe.bytesTotal);
						progressBar.label = (int(pe.bytesLoaded / pe.bytesTotal * 100) + 
							"% (" + pe.bytesLoaded + " of " + pe.bytesTotal + " bytes)");
					}
				
				var finishLoad:Function = function(e:Event=null):void{
						progressBar.visible = false;
						progressBar.setProgress(0,1);
						progressBar.includeInLayout = false;
						_dataSets.push(ds);
						timeline.refresh(true);
						_plotLayer.refresh(true);
						dispatchUpdated();
						if(onComplete != null) onComplete();
					};
			
				var ds:AirQualityDataSet = new AirQualityDataSet(source,pollutant,name);	
				if(source is Class) finishLoad();
				else{
					ds.addEventListener(ProgressEvent.PROGRESS, handleProgress);
					ds.addEventListener(Event.COMPLETE,finishLoad);
				} 
			}
			
			
			/**
			 * Forces all of the loaded datasets to reload from scratch.
			 */
			protected function reloadDataSets():void{
				var oldDataSets:Vector.<AirQualityDataSet> = new Vector.<AirQualityDataSet>();
				while(_dataSets.length > 0) oldDataSets.push(_dataSets.pop());
				for each(var d:AirQualityDataSet in oldDataSets){
					loadData(d.dataURI,d.pollutant,d.name);					
				}
			}
			
			
			/** Pops up a menu that allows markers to be
			 *   added to the display.
			 */
			protected function createMenu(mousePt:Point,dataPoint:Object):void{
				
				
				//Short-term solution, always center on point click, bypassing the menu
				if(dataPoint.lat && dataPoint.lat != "None") panAndZoomTo(new Location(dataPoint.lat, dataPoint.lon),map.getZoom());
				if(dataPoint.time) timeline.centerOn(dataPoint.time);
				
				
				/* TODO: Bring all of this code back once commenting is avaliable. 
				//set up menu XML
				var menuXML:XML = XML(<root/>);
				var centerXML:XML = XML(<item/>);
				centerXML.@label = 'Center Views on This Point';	
				centerXML.@event = 'center';
				menuXML.appendChild(centerXML);
				
				//TODO:Enable Commenting functionality.
				if(_selections.isSelected(dataPoint)){
					var viewXML:XML = XML(<item/>);
					viewXML.@label = 'View Comments for this Data Point';	
					viewXML.@icon = 'CommentIcon';
					viewXML.@event = 'view';
					menuXML.appendChild(viewXML);
				}
				else{
					var addXML:XML = XML(<item/>);
					addXML.@label = 'Comment on this data point';	
					addXML.@icon = 'CommentIcon';
					addXML.@event = 'add';
					menuXML.appendChild(addXML);
				}
				
				var cancelXML:XML = XML(<item/>);
				cancelXML.@label = 'Cancel';
				cancelXML.@icon = 'CancelIcon';
				cancelXML.@event = 'cancel';
				menuXML.appendChild(cancelXML);

				//build and display menu
				var menu:Menu = Menu.createMenu(map,menuXML,false);
				menu.labelField = "@label";
				menu.iconFunction = iconFunction;
				menu.addEventListener(MenuEvent.ITEM_CLICK,function(me:MenuEvent):void{
						//When Add is clicked - add selection
						if(me.item.@event == 'add'){
							var s:Object = _selections.addSelection(dataPoint.id);
							_plotLayer.refresh(true);
							timeline.refresh(true);
						}
						//When Center is clicked - center both views on point
						else if(me.item.@event == 'center'){
							if(dataPoint.lat && dataPoint.lat != "None") panAndZoomTo(new Location(dataPoint.lat, dataPoint.lon),map.getZoom());
							if(dataPoint.time) timeline.centerOn(dataPoint.time);
						}
						//When view is clicked, focus that selection
						else if(me.item.@event == 'view'){
							var dSel:Array = _selections.getSelections(dataPoint);
							if(dSel && dSel.length > 0) dispatchEvent(new EntityEvent(EntityEvent.ENTITY_FOCUSED,dSel[0]));
							
						}
						//otherwise, hide the menu
						else menu.hide();
					});
					
	            menu.show(mousePt.x, mousePt.y);*/
			}
			
			/**
			 * Pans and zooms to a specific point and zoom level.
			 *  If this takes longer than expected, cut to the chase and just jump 
			 *  to the specified loc/zoom - this circumvents a bug where modest maps
			 *  can hang indefinitely when panning.
			 */
			protected function panAndZoomTo(loc:Location,zoom:int):void{
				
				if(_isPanningAndZooming) return; //don't try to run if one is underway
				_isPanningAndZooming = true;
				var mapZoom:int = map.getZoom();
				
				//We'll skip to the point if the pan takes 50% longer than expected. 
				var t:Timer = new Timer(1000 * map.panDuration * 1.5);
				
				//If panning completes successfully, then zoom
				var onPZPan:Function = function(me:MapEvent):void{
						map.removeEventListener(MapEvent.STOP_PANNING,onPZPan);
						map.zoomByAbout(zoom - mapZoom);
					}
				
				//If zooming completes or the timer expires, do a final recenter and dispatch events
				var onPZComplete:Function = function(e:Event=null):void{
						
						//Remove listeners
						t.stop();
						t.removeEventListener(TimerEvent.TIMER,onPZComplete);
						map.removeEventListener(MapEvent.STOP_PANNING,onPZPan);
						map.removeEventListener(MapEvent.STOP_ZOOMING,onPZComplete);
						
						
						var newCenter:Location = map.getCenter();
						var newZoom:Number = map.getZoom();
						
						//Recenter if our zooming misaligns the result of the pan
						if(!locEquals(loc,newCenter)){
							map.setCenter(loc);
						} 
						if(newZoom != zoom){
							 map.setZoom(zoom);	
						}
						
						if(!locEquals(loc,newCenter) || map.getZoom() != zoom){
							newCenter = map.getCenter(); newZoom = map.getZoom();
							trace("Error: Center and Zoom still are not correct after realign!");
						}
						
						_isPanningAndZooming = false;
						map.dispatchEvent(new CSEvent(CSEvent.PAN_AND_ZOOM_COMPLETE));
					}				
							
				//Add Listeners
				map.addEventListener(MapEvent.STOP_PANNING, onPZPan);
				map.addEventListener(MapEvent.STOP_ZOOMING, onPZComplete);
				t.addEventListener(TimerEvent.TIMER, onPZComplete);
								
				//Start the panning an timer
				t.start();
				map.panTo(loc);
			}
			
			/**
			 * Center the map (currently on the last plotted data point)
			 */
			protected function centermap():void{
				var last:Object;
				for each(var d:AirQualityDataSet in _dataSets){
					if(d.hidden) continue;
					for(var di:int=0; di < d.data.length; di++){
						var dl:Object = d.data[di];
						if((!last || last.time < dl.time) && dl.lat && dl.lon && dl.lat!="None") last = dl;
						if(_plotLayer.maxTime > 0 && dl.time >= _plotLayer.maxTime) break;
					}	
				}
				if(last) panAndZoomTo(new Location(last.lat,last.lon), map.getZoom());
			}
			
			
			/******** Implements IStatefulVis *************************************************/	
				
			protected var _setStateReady:Boolean = false;	
			public function get setStateReady():Boolean{ return _setStateReady; }
				
			
			public function get state():Object{
				var cz:Array = map.getCenterZoom();
				var cl:Location = roundLocation(cz[0] as Location);
				var ds:String;	//dataset uris
				var dsn:String;	//dataset names
				var dsp:String;	//dataset pollutants
				var dsh:String;	//dataset hidden status
				var dsorted:Array = [];
				for each(var d:AirQualityDataSet in _dataSets) dsorted.push(d);
				dsorted.sortOn("dataURI"); 
				for each(var di:AirQualityDataSet in dsorted){
					ds = (ds == null ? di.dataURI : ds + "," + di.dataURI);
					dsn = (dsn == null ? escape(di.name) : dsn + "," + escape(di.name));
					dsp = (dsp == null ? escape(di.pollutant) : dsp + "," + escape(di.pollutant));
					dsh = (dsh == null ? di.hidden.toString() : dsh + "," + di.hidden.toString());
				} 
				return {centerLat:cl.lat,
						centerLon:cl.lon,
						zoom:cz[1],
						dataSource:ds,
						dataNames:dsn,
						dataPollutants:dsp,
						dataHidden:dsh,
						xBottom:timeline.xBottom,
						xTop:timeline.xTop,
						playheadPosition:timeline.playheadPosition};
			}
			
			public function set state(o:Object):void{
				
				//parameters to be set once we know the data's loaded
				var onceLoaded:Function = function():void{
						//pan and zoom appropriately
						panAndZoomTo(new Location(o.centerLat,o.centerLon),o.zoom);
						
						//set hidden/visibile for each dataset
						for each(var hds:AirQualityDataSet in _dataSets){
							if(dsn.indexOf(hds.name) != -1)
								hds.hidden = Boolean(dsh[dsn.indexOf(hds.name)]);
						}
						
						//set time bounds
						timeline.xBottom = o.xBottom;
						timeline.xTop = o.xTop;
						timeline.playheadPosition = o.playheadPosition;
						timeline.refresh();
					};

				//get names, uris, pollutants, and hidden status of datasets in state
				var ds:Array = (o.dataSource is String) ? (o.dataSource as String).split(',') : null;
				var dsn:Array = (o.dataNames is String) ? (o.dataNames as String).split(',') : null;
				var dsp:Array = (o.dataPollutants is String) ? (o.dataPollutants as String).split(',') : null;
				var dsh:Array = (o.dataHidden is String) ? (o.dataHidden as String).split(',') : null;
				
				var loadingCount:int = 0;
				
				//pull old datasets
				var oldDataSets:Vector.<AirQualityDataSet> = new Vector.<AirQualityDataSet>();
				while(_dataSets.length > 0) oldDataSets.push(_dataSets.pop());
				
				//loook for specified datasets via URI
				if(ds){
					for(var i:int=0;i<ds.length;i++){
						var dsi:String = ds[i];
						//if we have an old dataset that matches, keep it
						var match:Boolean = false;
						for each(var ods:AirQualityDataSet in oldDataSets){
							if(ods.dataURI == dsi){
								match = true;
								_dataSets.push(ods);
								break;
							}
						}
						//otherwise, load from outside
						if(!match){
							loadingCount++; 
							loadData(dsi,unescape(dsp[i]),unescape(dsn[i]),function():void{
									loadingCount--;
									if(loadingCount == 0)onceLoaded();
								});
						}
					}					
				}
				if(loadingCount == 0)onceLoaded();
				
			}
			
			protected function dispatchComplete(e:Event = null):void{
				_setStateReady = true;
				dispatchEvent(new StatefulVisEvent(StatefulVisEvent.VIS_CREATION_COMPLETE,true));
			}
			
			protected function dispatchUpdated(e:Event = null):void{
				if(_isPanningAndZooming) return; //don't dispatch during the middle of a panAndZoom() run
				dispatchEvent(new StatefulVisEvent(StatefulVisEvent.VIS_UPDATE,true));
			}
			
			public function getThumbnail():BitmapData{ // needed for new version of commentspace...
				return null;
			}
			
			/* Security settings - should be constrained before deployment!
			 *  These settings allow this code to be loaded into and accessed by 
			 *  a Flash movie on ANY external server. *****************************/
			Security.allowDomain("*");
			Security.allowInsecureDomain("*");
			
			
			/******** Helper Methods **********************************************************/
				
			public function onResize(event:Event):void
			{	
				var mc:Location = map.getCenter(); 
				map.setSize(this.width, this.height);
				map.setCenter(mc);
				
			}	
			
			/**Checks if two locations are equal to some level of precision */
			protected function locEquals(a:Location,b:Location,precision:uint=10):Boolean{
				return (a.lat.toPrecision(precision) == b.lat.toPrecision(precision) 
					&& a.lon.toPrecision(precision) == b.lon.toPrecision(precision));
			}
			
			/**Returns a location with lat/lon rounded to some level of precision */
			protected function roundLocation(l:Location,precision:uint=10):Location{
				return new Location(Number(l.lat.toPrecision(precision)),Number(l.lon.toPrecision(precision)));
			}
				
				
			/**
			 * A workaround which sets icons in the menus using a 
			 *  function since the standard @icon attribute in the
			 *  XML doesn't work unless the imports are at the application
			 *  root. 
			 * @param o
			 * @return 
			 * 
			 */		
			protected function iconFunction(o:Object):Class{
				if((o as XML).attributes().icon){
					var iconName:String = o.@icon;
					if(!iconName || iconName == "") return null;
					var iconClass:Class = this[iconName] as Class;
					return iconClass;				
				}
				else return null;
			}
			
			
			
			/**
			 * Triggers a disclaimer popup.
			 */
			protected function triggerDisclaimerPopup(onOkay:Function=null):void{
				var disclaimer:DisclaimerPanel = new DisclaimerPanel();
				disclaimer.onOkay = function():void{
						if(onOkay != null) onOkay();
						PopUpManager.removePopUp(disclaimer);
					};
				PopUpManager.addPopUp(disclaimer,this,true);
				PopUpManager.centerPopUp(disclaimer);
			}
			
			/**
			 * Triggers a popup display that allows data to be loaded from
			 *  outside.
			 */
			protected function triggerLoadingPopup():void{
				var dataPanel:DataLoadingPanel = new DataLoadingPanel();
				
				//when the okay button is clicked
				dataPanel.onLoadClick = function(e:Event=null):void{
						var info:Array = dataPanel.getSelectedInfo(map, timeline);
						if(info && info.length > 0){
							
							//clear the existing plots
							for(var ds:String in _dataSets) delete _dataSets[ds];
							_plotLayer.refresh(true);
							timeline.refresh(true);
							
							var pollutant:String = info[0].reading;
							for each (var i:Object in info) loadData(i.source,pollutant,i.name);
						} 
						PopUpManager.removePopUp(dataPanel);
					};
					//when the cancel button is clicked
					//when the okay button is clicked
				dataPanel.onCancelClick = function(e:Event=null):void{
						PopUpManager.removePopUp(dataPanel);
					};
				PopUpManager.addPopUp(dataPanel,this,true);
				PopUpManager.centerPopUp(dataPanel);
			}
			
		]]>
	</mx:Script>
	<mx:Style source="assets/commonsensestyles.css"/>
	
</mx:Application>
