<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:display="flash.display.*"  height="70"
	backgroundColor="0xDDCCCCCC" backgroundAlpha="0.8" 
	mouseUp="{stopPlayHeadDrag(event)}" rollOut="{stopPlayHeadDrag(event)}"
	horizontalScrollPolicy="off" verticalScrollPolicy="off">
	
	<mx:VRule id="tipLine" top="{margin}" bottom="{margin}" visible="false"/>
	
	<mx:Canvas id="container"  x="{margin + 45}" width="{width - (2 * margin) - 45}" 
		top="{margin}" height="{height - 2 * margin - SCROLLBAR_HEIGHT}" resize="{dirty.dirty();refresh()}" 
		mouseOver="{handleMouseMove(event)}" mouseOut="{handleMouseOut(event)}" 
		mouseMove="{handleMouseMove(event);playHeadDrag(event)}" click="{handleMouseClick(event)}" 
		doubleClick="{handleDoubleClick(event)}"
		horizontalScrollPolicy="off" verticalScrollPolicy="off"
		doubleClickEnabled="true" color="#aa2222"/>
		
	<mx:Canvas id="playhead" top="{margin}" bottom="{margin + SCROLLBAR_HEIGHT}" 
		x="{container.x + container.width}" width="6" useHandCursor="true"
		mouseChildren="false" buttonMode="true"
		mouseDown="{startPlayHeadDrag(event)}" 
		backgroundAlpha="0.4" backgroundColor="#FFFFFF">
		<mx:VRule x="{playhead.width / 2}" height="100%"/>
	</mx:Canvas>
	
	<mx:Canvas id="labels" mouseEnabled="false" x="{margin + 45}" width="100%" height="100%" />
		
	<mx:Button id="zOut" click="{zoomOut()}" label="-" toolTip="zoom out" 
			x="{margin}" y="{margin + 25}" paddingLeft="0" paddingRight="0" width="25"/>
	<mx:Button id="zIn" click="{zoomIn()}" label="+" 
			toolTip="zoom in" width="25" paddingLeft="0" 
			paddingRight="0" y="{margin}" x="{margin}"/>

	<mx:Button id="pauseBtn" x="{margin}" label="||" bottom="{margin}" enabled="{_playing}" 
		paddingRight="0" paddingLeft="0" click="{stopPlayback(event)}" width="18" 
		toolTip="pause"/>
	<mx:Button id="playBtn" x="{margin + 20}" label=">" bottom="{margin}" enabled="{!_playing}"
		paddingRight="2" paddingLeft="2" click="{startPlayback(event)}" width="20"
		toolTip="play"/>
	<mx:VSlider id="playSlider" x="{margin }" y="{playBtn.y - 60}" height="50" value="3"
		minimum="0.3" maximum="4" labels="{['.','..','...','....']}" dataTipFormatFunction="{playScrollDataTip}"
		toolTip="{'playback speed: ' + int(Math.pow(10,playSlider.value)) + 'x'}" liveDragging="true"
		change="{timeMultiplier=Math.pow(10,playSlider.value)}"/>
	
	
	<mx:Script>
		<![CDATA[
			import etc.DirtyFlag;
			import events.TimelineEvent;
			import mx.messaging.AbstractConsumer;
			import events.DataSetEvent;
			import mx.messaging.channels.PollingChannel;
			import etc.PointRenderer;
			import mx.events.CollectionEventKind;
			import mx.events.CollectionEvent;
			import data.SelectionSet;
			import data.AirQualityDataSet;
			import events.CSEvent;
			import etc.AirQualityColors;
			import events.DataPointEvent;
			import mx.controls.ToolTip;
			import mx.core.IToolTip;
			import mx.managers.ToolTipManager;
			import fl.events.ScrollEvent;
			import fl.controls.ScrollBarDirection;
			import fl.controls.ScrollBar;
			import __AS3__.vec.Vector;
			
			//------------- Public variables -----------------------------------------------
			public var xDataField:String = 'time';
			public var yDataField:String = 'value';
			public var margin:Number = 15;
			public var spacing:Number = 5;
			public var pointDiameter:Number = 6;
			public var pointOverlapTolerance:Number = 6;
			public var zoomPercentage:Number = 0.6;
			//public var numMouseOverAdjacents:uint = 15;
			
			
			/**
			 * A data object should be an Object containing
			 *  sets of points (as Vector.<Object>s) 
			 *  indexed by the URI of that set of points.  
			 * @param d
			 */	
			public function get dataSets():Vector.<AirQualityDataSet>{return _dataSets;}
			public function set dataSets(d:Vector.<AirQualityDataSet>):void{
				_dataSets = d;
				refresh(true);
			}
			
			/**
			 * A set of selections to draw.
		 	 */		
			public function get selections():SelectionSet{ return _selections;}
			public function set selections(s:SelectionSet):void{
				_selections = s;
				s.addEventListener(CollectionEvent.COLLECTION_CHANGE, function(ce:CollectionEvent):void{
						dirty.dirty();
						refresh();
					});
			}
			
			/**
			 * The current location of the playhead in terms of the xDataField
			 */
			[Bindable(event=TimelineEvent.PLAYHEAD_MOVED)]
			public function get playheadPosition():Number{
				if(!isNaN(_playheadPosition)) return _playheadPosition 					
				else if(!isNaN(xTop)) return xTop;
				else return 0;
			}
			public function set playheadPosition(val:Number):void{
				var oldVal:Number = _playheadPosition;
				_playheadPosition = Math.max(xBottom, Math.min(xTop, val));
				
				if(_playheadPosition > oldVal) dirty.dirty(DirtyFlag.APPEND);
				else dirty.dirty();
				
				if(_playheadPosition != oldVal){
					dispatchEvent(new TimelineEvent(TimelineEvent.PLAYHEAD_MOVED,_playheadPosition));
					if(!_playing) dispatchEvent(new TimelineEvent(TimelineEvent.PLAYHEAD_STOPPED,_playheadPosition));
				}
			}
			
			/**
			 * The value from the xDataField corresponding
			 *  to the right edge of the currently visible section of the timeline.
			 */
			public function get xBottom():Number { return _xBottom; }
			public function set xBottom(value:Number):void{
				_xBottom = value;
				dirty.dirty();
			}
			/**
			 * The value from the xDataField corresponding
			 *  to the right edge of the currently visible section of the timeline.
			 */
			public function get xTop():Number { return _xTop; }
			public function set xTop(value:Number):void {
				_xTop = value;
				dirty.dirty();
			}
			
			
			
			//------------- Data Management variables -----------------------------------------------
			protected var _dataSets:Vector.<AirQualityDataSet>;
			protected var yMin:Number;		//the lowest data value on the y axis
			protected var yMax:Number; 		//the highest data value on the y axis
			protected var xMin:Number;		//the lowest data value on the x axis
			protected var xMax:Number;		//the lowest data value on the x axis
			
			protected var _xBottom:Number;	//the bottom end of the current display x-axis
			protected var _xTop:Number;		//the top end of the current display x-axis
			
			protected var _playheadPosition:Number; //the position of the playhead on the x axis.
			
			protected var _dSetBounds:Object = {} //the screen bounds of the regions reserved for each dataset (indexed by URI)
			protected var _dSetLabels:Object = {} //labels for each dataset's region (indexed by URI)
			protected var _selections:SelectionSet; //the set of selections to draw in the current display 
			
			protected var _bitmap:Bitmap;
			protected var _mask:Sprite = new Sprite();
			protected var _hScroll:ScrollBar = new ScrollBar();
						
			protected var _prevMaxPtNums:Object = {}; 	//last point number drawn by the last append pass (indexed by dataURI)
			protected var _prevPos:Object = {} 			//position of the last point drawn (indexed by dataURI)
		
			protected var dirty:DirtyFlag = new DirtyFlag();
		
			protected const SCROLLBAR_HEIGHT:Number = 18;
			
			//-------------Public Methods -----------------------------------------------------

			public function refresh(force:Boolean=false):void{
				if(force) calculateMinMax();
				if(isNaN(yMin) || isNaN(yMax)) return;
				else plotData();
			}
			
			
			//------------- Protected Methods -----------------------------------------------------
				
			protected function plotData(e:Event=null):void{
				
				if(!container) return;

				//If no data do nothing
				if(!dataSets || dataSets.length == 0){
					while(container.rawChildren.numChildren > 0)container.rawChildren.removeChildAt(0);
					plotLabels();
					return;
				}
				
				if(dirty.check(DirtyFlag.CLEAN)) return;
				
				//if marked dirty (not just adding a few points) clean up
				if(dirty.check(DirtyFlag.DIRTY)){
					_prevMaxPtNums = {};
					_prevPos = {};
					while(container.rawChildren.numChildren > 0)container.rawChildren.removeChildAt(0);
				} 
				
				//prep the renderer and bitmap data we'll use to draw the points
				var plotBitmapData:BitmapData
				if(dirty.check(DirtyFlag.DIRTY) || !_bitmap){
					if(plotBitmapData) plotBitmapData.dispose();
					plotBitmapData = new BitmapData(container.width * 2,container.height,true,0x00FFFFFF);
				} 
				else plotBitmapData = _bitmap.bitmapData;
				var renderer:PointRenderer = new PointRenderer(plotBitmapData,pointDiameter,pointOverlapTolerance);
				
				//draw outside the current range so that scrolling 
				// cuts less abruptly
				var curRange:Number = _xTop - _xBottom;
				var xbd:Number = Math.max(xMin,_xBottom - curRange);
				var xtd:Number = Math.min(xMax,_xTop + curRange);
				
				//allocate space for each point set
				_dSetBounds = {};
				var psHeight:Number = (container.height -  
					(dataSets.length - 1) * spacing) / dataSets.length;
				var psBounds:Rectangle = new Rectangle(0,0,container.width,psHeight);

				plotBitmapData.lock();
				
				if(_dataSets){
										
					//for each of the sets of points...
					for each(var ds:AirQualityDataSet in _dataSets){
						var pSet:Vector.<Object> = ds.data;
						
						//set bounds for the set
						_dSetBounds[ds.dataURI] = psBounds.clone();
						if(dirty.check(DirtyFlag.DIRTY)) plotAxes(plotBitmapData,psBounds);
						psBounds.y += (psHeight + spacing);
						
						//skip if no data
						if(!ds.data) continue;
						
						//start from the last drawn point if possible					
						var playheadPt:Number = NaN;
						var firstPoint:int =  _prevMaxPtNums[ds.dataURI] ?  _prevMaxPtNums[ds.dataURI] : 0;
						
						//iterate through points
						for(var i:int=firstPoint; i < ds.data.length; i++){
							var point:Object = ds.data[i]; 
							if(point.time < xbd || point.time > xtd) continue;
							
							//Save the index of the last point drawn at/after the playhead 							
							if(point.time > playheadPosition && isNaN(playheadPt)){
								playheadPt = i;
								//We can skip everything after the playhead if we've already drawn it
								if(_prevMaxPtNums[ds.dataURI] != null) break;
							} 
														
							//determine location
							var dPt:Point = datapointToLocalPosition(point);
							
							//skip if position not different different from last plotted point
							if(!selections.isSelected(point) && _prevPos[ds.dataURI]
									&& Math.abs(_prevPos[ds.dataURI].x - dPt.x) < pointOverlapTolerance 
									&& Math.abs(_prevPos[ds.dataURI].y - dPt.y) < pointOverlapTolerance){
								continue;
							}  
							
							//plot the point to the current bitmapdata
							renderer.plotPoint(point,dPt,ds.pollutant,selections.isSelected(point),false,(point.time > playheadPosition));
							_prevPos[ds.dataURI] = dPt;
						}
						if(!isNaN(playheadPt)) _prevMaxPtNums[ds.dataURI] = playheadPt;
					}
					
					plotBitmapData.unlock();
					
					if(dirty.check(DirtyFlag.DIRTY)){
						_bitmap = new Bitmap(plotBitmapData);
						_bitmap.x = -(container.width / 2);
						container.rawChildren.addChild(_bitmap);
					}
				}

				//position playhead
				playhead.x = (playheadPosition - xBottom) / (xTop - xBottom) * container.width + container.x;				
				//set up and position the labels, scrollBar, and mask
				plotLabels();
				setupMask();
				setupScrollBar();
				
				dirty.clean();
			}
			
			/**
			 * Plots the X and Y axes and gridlines for all 
			 *  of the datasets shown in the timeline.
			 */
			protected function plotAxes(bd:BitmapData,bounds:Rectangle):void{
				var plotShape:Shape = new Shape();
				
				//plot x baseline
				plotShape.graphics.lineStyle(0.5,0x999999);
				plotShape.graphics.moveTo(0,bounds.bottom);
				plotShape.graphics.lineTo(bd.width,bounds.bottom);
				
				var startDate:Date = new Date(_xBottom * 1000);
				startDate.setFullYear(startDate.fullYear,startDate.month,startDate.day + 1);
				startDate.setHours(0,0,0,0);
				
				//plot days
				var d:Number = startDate.time / 1000;
				plotShape.graphics.lineStyle(0.5,0x999999,0.5);
				while(d < _xTop){
					var dx:Number = (d - _xBottom) * bounds.width / 
							(_xTop - _xBottom) + bounds.width / 2;
					plotShape.graphics.moveTo(dx,bounds.bottom);
					plotShape.graphics.lineTo(dx,bounds.top);
					d += (24 * 3600);
				}
				
				//plot hours
				d = startDate.time / 1000;
				plotShape.graphics.lineStyle(0.5,0x999999,0.2);
				while(d < _xTop){
					var dx:Number = (d - _xBottom) * bounds.width / 
							(_xTop - _xBottom) + bounds.width / 2 ;
					plotShape.graphics.moveTo(dx,bounds.bottom);
					plotShape.graphics.lineTo(dx,bounds.top);
					d += 3600;
				}
				
				//draw into bitmap data
				bd.draw(plotShape);				
			}
			
			/**
			 * Manages the labels for each of the different datasets shown in the timeline
			 */
			protected function plotLabels():void{
				//remove unused labels
				for(var dl:Object in _dSetLabels){
					if(_dSetLabels[dl] && (!_dSetBounds[dl] || !_dataSets || _dataSets.length == 0)){
						labels.removeChild(_dSetLabels[dl]);						
						_dSetLabels[dl] = null;	
					}	
				}
				
				//add and update others
				for each(var d:AirQualityDataSet in _dataSets){
					if(!_dSetBounds[d.dataURI]) continue;
					if(!_dSetLabels[d.dataURI]) _dSetLabels[d.dataURI] = new TimelineLabel();
					var l:TimelineLabel = _dSetLabels[d.dataURI];
					if(d.name) l.labelText = d.name;
					l.y = _dSetBounds[d.dataURI].top + margin;
					labels.addChild(l);
					l.onRemoveClick = function(e:MouseEvent):void{
							// a very shoddy way of finding the dataset to remove.
							//  however, just using 'd' doesn't work because of a scoping issue
							for each(var dr:AirQualityDataSet in _dataSets){
								if(dr.name == (e.target.parent as TimelineLabel).labelText){
									if(_dataSets.lastIndexOf(dr) != -1){
										_dataSets.splice(_dataSets.lastIndexOf(dr),1);
										dispatchEvent(new DataSetEvent(DataSetEvent.DATASET_REMOVED,dr));
									}
								}								
							}
						};
				}	
			}

			/**
			 * Setup and place the mask for the bitmap 
			 */ 
			protected function setupMask():void{
				_mask.graphics.clear();
				_mask.graphics.beginFill(0xFFFFFF);
				_mask.graphics.drawRect(-(pointDiameter / 2),-(pointDiameter / 2),
					container.width + pointDiameter, container.height + SCROLLBAR_HEIGHT + pointDiameter);
				_mask.graphics.endFill();
				
				container.rawChildren.addChild(_mask);
				
				if(container.mask != _mask)container.mask = _mask;
			}
			
			
			/**
			 * Set up and place the scroll bar
			 */
			protected function setupScrollBar():void{
				_hScroll.direction = ScrollBarDirection.HORIZONTAL;
				_hScroll.x = -(pointDiameter / 2);
				_hScroll.y = container.height - _hScroll.height + SCROLLBAR_HEIGHT;
				_hScroll.width = container.width + pointDiameter;
				
				var range:Number = _xTop - _xBottom;
				
				_hScroll.setScrollProperties(range,xMin,xMax-range,range / 10);
				_hScroll.scrollPosition = _xBottom;
				
				if(!_hScroll.hasEventListener(ScrollEvent.SCROLL)){
					_hScroll.addEventListener(ScrollEvent.SCROLL, handleScroll);
				}
								
				container.rawChildren.addChild(_hScroll);
			}
			
			
			protected var _scrollTimer:Timer;
			protected function handleScroll(se:ScrollEvent):void{	
				var range:Number = _xTop - _xBottom;
				var scrollBy:Number = ((se.position - _xBottom) * container.width / range);
				_bitmap.x -= scrollBy;  
				playhead.x -= scrollBy;
				
				_xBottom = Math.max(_hScroll.scrollPosition,xMin);
				_xTop = Math.min(_xBottom + range ,xMax);

				//hide the playhead if it goes out of view
				if(playhead.x < container.x || playhead.x > (container.x + container.width))playhead.visible = false;
				
				//Only redraw periodically while scrolling	
				if(_scrollTimer && _scrollTimer.running){
					_scrollTimer.reset();
					_scrollTimer.start();
				} 
				else{
					_scrollTimer = new Timer(300);
					_scrollTimer.addEventListener(TimerEvent.TIMER,function(e:Event):void{
							_scrollTimer.stop();
							playheadPosition = playheadPosition;
							playhead.visible = true;
							dispatchEvent(new TimelineEvent(TimelineEvent.TIMELINE_SCROLLED,playheadPosition));
							dirty.dirty();
							refresh();
						});
					_scrollTimer.start();
				}
			}
			
			/**
			 * Attempts to center the timeline on the given value
			 *  without changing the zoom level. 
			 */
			public function centerOn(midPoint:Number):void{
				var range:Number = _xTop - _xBottom;
				if(range < xMax - xMin){
					_xBottom = Math.max(xMin,int(midPoint - range / 2));
					_xTop = Math.min(xMax,int(midPoint + range / 2));
					playheadPosition = playheadPosition;
					dirty.dirty();
					refresh();
				}
			}
			
			/**
			 * Zooms the view in. If no midpoint is specified, the zoom is 
			 *  centered at the middle of the current view.
			 */
			public function zoomIn(midPointVal:Number=Number.POSITIVE_INFINITY):void{
				var zoomRange:Number = (_xTop - _xBottom) * zoomPercentage;
				var midPoint:Number = midPointVal != Number.POSITIVE_INFINITY ? midPointVal : _xBottom + (_xTop - _xBottom) / 2;
				_xBottom = Math.max(xMin,int(midPoint - zoomRange / 2));
				_xTop = Math.min(xMax,int(midPoint + zoomRange / 2));
				playheadPosition = playheadPosition;
				dispatchEvent(new TimelineEvent(TimelineEvent.TIMELINE_ZOOMED,playheadPosition));
				refresh();
			}
			
			/**
			 * Zooms the view out.
			 */
			public function zoomOut():void{
				var zoomRange:Number = (_xTop - _xBottom) / zoomPercentage;
				var midPoint:Number = _xBottom + (_xTop - _xBottom) / 2;
				_xBottom = Math.max(xMin,midPoint - zoomRange / 2);
				_xTop = Math.min(xMax,midPoint + zoomRange / 2);
				playheadPosition = playheadPosition;
				dispatchEvent(new TimelineEvent(TimelineEvent.TIMELINE_ZOOMED,playheadPosition));
				refresh();
			}
			
			
			/****************************** Tooltipping Methods ************************************/
			protected var _plotTip:ToolTip;
			
			/**
			 * Draws and positions a tooltip for the given datapoint.
			 */
			public function drawDataTip(dataPoint:Object):void{
				
				var ptPosition:Point = datapointToLocalPosition(dataPoint);
				if(!ptPosition) return;
				ptPosition = container.localToGlobal(ptPosition);
				if(!_plotTip)_plotTip = ToolTipManager.createToolTip(dataPoint.value,ptPosition.x,ptPosition.y) as ToolTip;
			
				//color the tip and add additional text if possible
				if(dataPoint.value){
					//FIXME: Currently defaulting to pollutant from the first dataset, should be able to support per-track
					var cat:String = AirQualityColors.getAQICategoryForValue(dataSets[0].pollutant,dataPoint.value);
					var color:uint = AirQualityColors.getColorForAQICategory(cat);
					_plotTip.setStyle("backgroundColor",color);
					_plotTip.setStyle("backgroundAlpha",0.7);
					var time:Number = Number(dataPoint.time * 1000);
					var date:Date = new Date(time);
					var value:Number = Number(dataPoint.value);
					_plotTip.text = date.toLocaleDateString() + "\n" +
						date.toLocaleTimeString() + "\n" + value.toPrecision(5) + " (" + cat + ")" +
						((dataPoint.lat == null || dataPoint.lat == "None") ? "\n!NO GPS DATA" : "");
					var rightInset:Number = Math.max(0, ptPosition.x + _bitmap.x -
						 container.localToGlobal(new Point(container.width - _plotTip.width)).x);
					_plotTip.x = ptPosition.x + _bitmap.x - rightInset;
					_plotTip.y = ptPosition.y;
					
					_plotTip.graphics.clear();
					
					//Overplot nearby points as part of the tooltip
					/*var pn:int = dataToPointNum(dataPoint);
					if(pn != -1){
						for(var pi:int = pn - numMouseOverAdjacents; pi < pn + numMouseOverAdjacents; pi++){
							if(pi < 0 || pi >= data.length) continue;
							var adjPt:Object = data[pi];
							if(adjPt && adjPt.lat != null && adjPt.lat !="None"){
								var aColor:uint = AirQualityColors.getColorForValue(dataSets[0].pollutant,adjPt.value);
								var aPosition:Point = container.localToGlobal(dataToPosition(adjPt));
								_plotTip.graphics.lineStyle(2,0xffffff,0.7);
								_plotTip.graphics.beginFill(aColor,1);
								_plotTip.graphics.drawCircle(aPosition.x - ptPosition.x, aPosition.y - ptPosition.y,pointDiameter - (pointDiameter/2 * Math.abs(pi - pn)/numMouseOverAdjacents));
				    			_plotTip.graphics.endFill();								
							}
						}
					}*/
					
					if(rightInset > 0){
						trace(rightInset);
					}
					PointRenderer.drawPointToGraphics(_plotTip.graphics,dataPoint,new Point(rightInset,0),
						dataSets[0].pollutant,selections.isSelected(dataPoint),true,false,pointDiameter);
					//Plot an accentuated version of the point as part of the tooltip
					/*_plotTip.graphics.lineStyle(2,0xffffff,0.7);
					_plotTip.graphics.beginFill(color,1);
					_plotTip.graphics.drawCircle(rightInset,0,pointDiameter);
	    			_plotTip.graphics.endFill();
	    			_plotTip.graphics.beginFill(0xffffff,1);
	    			_plotTip.graphics.drawCircle(rightInset,0,pointDiameter/2);
	    			_plotTip.graphics.endFill();*/
	    			
				}
				else _plotTip.text = "No Data";
			}	
				
				
			public function discardActiveDataTip():void{
				if(_plotTip){
					ToolTipManager.destroyToolTip(_plotTip);
					_plotTip = null;
					dispatchEvent(new DataPointEvent(DataPointEvent.UNHOVER));
				}
			}
			
			protected function handleMouseClick(me:MouseEvent):void{
				//if we've clicked on some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//locate the nearest matching point and trigger tooltipping
				var closestPoint:Object = posToClosestPoint(new Point(me.localX,me.localY),pointDiameter/2,pointDiameter/2);
							
				//If we have a point, fire an event
				if(closestPoint) dispatchEvent(new DataPointEvent(DataPointEvent.CLICK,closestPoint,new Point(me.stageX,me.stageY)));
			}
			
			
			protected function handleDoubleClick(me:MouseEvent):void{
				//if we've clicked on some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//calculate the location of the click and zoom in at that point
				var xVal:Number = localToDataSpace(new Point(me.localX,0)).x;
				if(xVal > _xBottom && xVal < _xTop) zoomIn(xVal);
			}
			
			
			protected function handleMouseMove(me:MouseEvent):void{
				
				//if we've moused over some other child (like the scrollbar) do nothing
				if(me.target != container) return;
				
				//locate the nearest matching point in x column and trigger tooltipping
				var closestPoint:Object = posToClosestPoint(new Point(me.localX,me.localY),pointDiameter/2);
				if(closestPoint){
					drawDataTip(closestPoint);
					tipLine.x = me.localX + container.x;
					tipLine.visible = true;
					dispatchEvent(new DataPointEvent(DataPointEvent.HOVER,closestPoint,new Point(me.stageX,me.stageY)));
				} 
				//If no point is sufficiently close throw away any tip we might have
				else{
					discardActiveDataTip();
					tipLine.visible = false;
				} 
				
			}
			
			protected function handleMouseOut(me:MouseEvent):void{
				tipLine.visible = false;
				discardActiveDataTip();
			}
			
			
			
			/****************************** Playback Methods ************************************/

			[Bindable]
			protected var _playing:Boolean = false;			
			protected var _playheadDragging:Boolean = false;
			protected var _timer:Timer;
			
			public var framerate:Number = 10; 	//as FPS	
			public var timeMultiplier:Number = 1000;
			
			protected function startPlayback(e:Event=null):void{
				//if playhead is maxed out, restart from the beginning.
				if(playheadPosition >= xTop){
					playheadPosition = xBottom;
				}
				//set up and run timer to push playhead forward
				_timer = new Timer(1000/framerate);
				_timer.addEventListener(TimerEvent.TIMER,function(te:TimerEvent):void{
						var increment:Number = (_timer.delay * timeMultiplier / 1000);
						playheadPosition += increment;
						if(playheadPosition >= xTop) stopPlayback();
						refresh();
					});
				_timer.reset();
				_timer.start();
				_playing = true;
			}
			
			/**
			 * Stops playback if it's currently going and dispatches a PLAYHEAD_STOPPED event.
			 */
			protected function stopPlayback(e:Event=null):void{
				if(_timer)_timer.stop();
				_playing = false;
				dispatchEvent(new TimelineEvent(TimelineEvent.PLAYHEAD_STOPPED,playheadPosition));
			}
			
			protected function startPlayHeadDrag(e:MouseEvent):void{
				_playheadDragging = true;
				playhead.setStyle("backgroundColor",0x62d000);
			}
			
			protected function stopPlayHeadDrag(e:MouseEvent):void{
				if(_playheadDragging){
					_playheadDragging = false;
					playhead.setStyle("backgroundColor",0xffffff);
					stopPlayback();
				}
			}
			
			protected function playHeadDrag(e:MouseEvent):void{
				if(_playheadDragging){
					playheadPosition = localToDataSpace(new Point(e.localX,0)).x;
					refresh();
				}	
			}
			
			
			/****************************** Helper Methods ************************************/
			
			protected function calculateMinMax():void{
				
				xMin = yMin = _xBottom = Number.POSITIVE_INFINITY; 
				xMax = yMax = _xTop = Number.NEGATIVE_INFINITY;
				
				for each(var ds:AirQualityDataSet in dataSets){
					//Find mins and maxes
					for each(var o:Object in ds.data){

						var xVal:Number = Number(o[xDataField]);
						if(isNaN(xMin) || xVal < xMin) xMin = xVal;
						if(isNaN(xMax) || xVal > xMax) xMax = xVal;

						var yVal:Number = Number(o[yDataField]);
						if(isNaN(yMin) || yVal < yMin) yMin = yVal;
						if(isNaN(yMax) || yVal > yMax) yMax = yVal;
					}
				}
				_xBottom = xMin;
				_xTop = xMax;
				
				dirty.dirty();
			}
			
			/**
			 * Converts a coordinate within the bounds of a dataset's 
			 * 	layout into a point in the data space.
			 *  If no dataset is specified, uses the container bounds instead 
			 *  (this gives valid x coordinates, but y values are incorrect
			 *  if more than one dataset is loaded).
			 */
			protected function localToDataSpace(position:Point,dataSet:AirQualityDataSet=null):Point{
				var bounds:Rectangle = (dataSet ? _dSetBounds[dataSet.dataURI] : 
					new Rectangle(container.x,container.y,container.width, container.height));
				
				var dPos:Point = position.clone();
				dPos.x = _xBottom + dPos.x * (_xTop - _xBottom) / bounds.width;
				dPos.y = yMin + (bounds.y + bounds.height - dPos.y) * (yMax - yMin) / bounds.height;
				return dPos;
			}
			
			
			/**
			 * Converts a coordinate in the data space
			 *  into a point in the coordinate space of the 
			 *  corresponding dataset's layout bounds. 
			 */
			protected function dataspaceToLocal(value:Point,dataSet:AirQualityDataSet):Point{
				var bounds:Rectangle = _dSetBounds[dataSet.dataURI];
				if(!bounds) return null; 
				
				var dv:Point = value.clone();
				dv.x = (dv.x - xBottom) / (xTop - xBottom) * bounds.width;
				dv.y = (yMax - dv.y) / yMax * bounds.height;				 
				return dv;
			}
			
			/**
			 * Returns the position of a datapoint within the bounds
			 *  of its parent dataset.
			 */
			protected function datapointToLocalPosition(pointData:Object):Point{
				if(!(_dSetBounds[pointData.sourceURI] is Rectangle)) return null;
				var sBounds:Rectangle = _dSetBounds[pointData.sourceURI];
				
				return new Point((pointData[xDataField] - _xBottom) * sBounds.width /
						 	(_xTop - _xBottom) + sBounds.width / 2,
						 sBounds.height * (1 - (pointData[yDataField] - yMin) /
						 	(yMax - yMin)) + sBounds.y); 
			}
			
			
			/**
			 * Returns the datapoint closest to a given position 
			 *  (provided in the coordinate space of the container).
			 */
			protected function posToClosestPoint(pos:Point,
				maxXDistance:Number=Number.POSITIVE_INFINITY,
				maxYDistance:Number=Number.POSITIVE_INFINITY):Object{
				
				//convert x max distance into data coordinate space
				if(maxXDistance != Number.POSITIVE_INFINITY){
					maxXDistance = localToDataSpace(new Point(maxXDistance,0)).x - localToDataSpace(new Point(0,0)).x;
					maxXDistance = Math.abs(maxXDistance);
				}

				//check which bounds (if any) the point is inside of
				for(var db:String in _dSetBounds){
					if((_dSetBounds[db] as Rectangle).containsPoint(pos)){
						
						//find the dataset for the enclosing bounds
						for each(var d:AirQualityDataSet in _dataSets){
							if(d.dataURI == db){
								//find the value of the cursor position in that space
								var overValue:Point = localToDataSpace(pos,d);
								
								var pts:Array = [];
								for each(var dx:Object in d.data){
									if(Math.abs(dx[xDataField] - overValue.x) < maxXDistance) pts.push(dx);
								}
								
								//convert y max distance into data coordinate space
								if(maxYDistance != Number.POSITIVE_INFINITY){
									maxYDistance = localToDataSpace(new Point(0,maxYDistance),d).y 
										- localToDataSpace(new Point(0,0),d).y;
									maxYDistance = Math.abs(maxYDistance);
								}
								
								//then find the closest of the datapoints in y;								
								var cpt:Object;
								var cdist:Number = Number.POSITIVE_INFINITY;
								for each(var dy:Object in pts){
									var dist:Number = Math.abs(dy[yDataField] - overValue.y);
									if(dist < cdist && dist < maxYDistance){
										cpt = dy;
										cdist = Math.abs(dy[yDataField] - overValue.y);
									} 
								}
								return cpt;
							}
						}
					}
				}
				return null;
			}
			
			
			/**
			 * Datatip function for playhead speed slider.
			 */
			protected function playScrollDataTip(val:Number):String{
				return int(Math.pow(10,val)) + 'x';
			}
			
		]]>
	</mx:Script>
	
	
</mx:Canvas>
